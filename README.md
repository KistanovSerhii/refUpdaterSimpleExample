# refUpdaterSimpleExample
Обновляет справочник у которого все реквизиты текстового типа

 1. Создаем справочник, например "Коды Б1"
 2. Добавляем справочнику форму списка, копируем в форму списка код
 
#Область КодМодульФормыИзКоторойВызываетсяФормаЗагрузки
// Пример открытия формы загрузки (это например форма списка справочника):
// просто после закрытия пользователю сообщит информацию выполнения.
//
#Область ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	имяКоманды		= "ЗагрузитьВСправочник";
	загКоманды		= "Загрузить";
	имяПроцедуры	= "ЗагрузитьВСправочник";
	
	ДобавитьНаФормуКомандуЗагрузки(имяКоманды, загКоманды, имяПроцедуры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПослеЗакрытияФормыЗагрузки(ПараметрЗакрытия, ДополнительныеПараметры) Экспорт 
	
	Если ПараметрЗакрытия = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ПараметрЗакрытия) = Тип("Структура") Тогда
		
		Если НЕ ПараметрЗакрытия.СправочникОбнавлен Тогда
			Возврат;
		КонецЕсли;
		
		текстСообщения = СтрШаблон("Справочник обновлен %1, создано %2, обновлено %3",
		ПараметрЗакрытия.СправочникОбнавлен,
		ПараметрЗакрытия.колСоздан,
		ПараметрЗакрытия.колИзменен);
		
		Сообщить(текстСообщения);
		
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВСправочник(Команда)
	
	ОткрытьФорму(
	"Справочник.КодыБ1.Форма.ФормаЗагрузки",
	,
	ЭтаФорма,
	ЭтотОбъект,
	,
	,
	Новый ОписаниеОповещения("ПослеЗакрытияФормыЗагрузки",ЭтаФорма)
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура Добавить На Форму Команду Загрузки
// имя			- Строка
// заголовок	- Строка
// имяПроцедуры	- Строка - имя процедуры которая будет вызвана (пример: Процедура ДобавитьВСправочник(Команда))
//
&НаСервере
Процедура ДобавитьНаФормуКомандуЗагрузки(имя, заголовок, имяПроцедуры)

	// Команда "аналог реквизит формы"
	НоваяКоманда 			= ЭтаФорма.Команды.Добавить(имя);
	НоваяКоманда.Заголовок	= заголовок;
	НоваяКоманда.Действие	= имяПроцедуры;	
	
	// Комадна "аналог элемент формы"
	НовыйЭлемент 					= ЭтаФорма.Элементы.Добавить(имя, Тип("КнопкаФормы"), ЭтаФорма.КоманднаяПанель);
	НовыйЭлемент.ИмяКоманды			= имя;
	НовыйЭлемент.Заголовок			= заголовок;
	НовыйЭлемент.Отображение		= ОтображениеКнопки.КартинкаИТекст;
	НовыйЭлемент.Картинка			= БиблиотекаКартинок.КонструкторЗапроса;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

3. Создаем произвольную форму для этого справочника и копируем в модуль формы код:
// Все что необходимо:
// 1. Добавить в модуль формы код:
//
#Область КодМодуляФормы


#Область ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	имяКоманды		= "ДобавитьВСправочник";
	загКоманды		= "Добавить в справочник";
	имяПроцедуры	= "ДобавитьВСправочникКоманда";
	имяТабДокумента	= "ТабДокумент";
	описаниеТаблицы = ПолучитьОписаниеКолонокНаСервереБезКонтекста();

	ДобавитьНаФормуРеквизитыДляИнформированияОРезультате();	
	ДобавитьНаФормуРеквизитТабличныйДокументНаСервере(имяТабДокумента);
	ДобавитьНаФормуРеквизитТаблицаЗначенийНаСервере(описаниеТаблицы);
	ДобавитьНаФормуКомандуЗагрузки(имяКоманды, загКоманды, имяПроцедуры);
	
	ОтобразитьЗаголовокТабличногоДокумента(ЭтаФорма.ТабДокумент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИмяТаблицыФормы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьВСправочникКоманда(Команда)
	
	ЭтаФорма.колСоздан	= 0;
	ЭтаФорма.колИзменен	= 0;
	ЭтаФорма.ТабЗначений.Очистить();
	
	ПереборТабличногоДокументаНаКлиентеЗаполняяТЗ(ЭтаФорма.ТабДокумент, ЭтаФорма.ТабЗначений);
	
	Если ЭтаФорма.ТабЗначений.Количество() = 0 Тогда
		ЭтаФорма.СправочникОбнавлен = Ложь;
		Возврат;
	КонецЕсли;
	
	результат 			= ДобавитьВСправочникНаСервере();
	ЭтаФорма.колСоздан	= результат.колСоздан;
	ЭтаФорма.колИзменен	= результат.колИзменен;
	
	Если результат.колСоздан > 0 ИЛИ результат.колИзменен > 0 Тогда	
		
		ЭтаФорма.СправочникОбнавлен = Истина;
		ПараметрыЗакрытия 			= ПолучитьПарамертрыЗакрытия();
		
		ЭтаФорма.Закрыть(ПараметрыЗакрытия);
	Иначе
		Сообщить("Нет данных для обновления/добавления!");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьНаФормуРеквизитыДляИнформированияОРезультате()
	
	ДобавляемыеРеквизиты = Новый Массив;
		
	НовыйРеквизит = Новый РеквизитФормы("колИзменен",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,0)));	
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	
	НовыйРеквизит = Новый РеквизитФормы("колСоздан",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,0)));	
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	
	НовыйРеквизит = Новый РеквизитФормы("СправочникОбнавлен",Новый ОписаниеТипов("Булево")); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
КонецПроцедуры

// Процедура Добавить На Форму Реквизит Табличный Документ На Сервере
//
// имяТабДокумента - Строка - будет создан реквизит и элемент формы с данным наименованием
//
&НаСервере
Процедура ДобавитьНаФормуРеквизитТабличныйДокументНаСервере(имяТабДокумента)

	реквизит 	= Новый РеквизитФормы(имяТабДокумента, новый ОписаниеТипов("ТабличныйДокумент"));
	МассивРекв 	= Новый Массив;
	
	МассивРекв.Добавить(реквизит);
	ЭтаФорма.ИзменитьРеквизиты(МассивРекв);
	
	НовПоле 					= Элементы.Добавить(имяТабДокумента, Тип("ПолеФормы"));
	НовПоле.Вид 				= ВидПоляФормы.ПолеТабличногоДокумента;
	НовПоле.ПутьКДанным 		= имяТабДокумента;
	
	ЭтаФорма.Элементы[имяТабДокумента].ОтображатьСетку	= Истина;
	ЭтаФорма.Элементы[имяТабДокумента].Редактирование	= Истина;	
	
КонецПроцедуры

// Процедура Добавить На Форму Реквизит Таблица Значений На Сервере
//
// описаниеТаблицы - Структура ИЗ
// *Наименование - Строка
// *Заголовок - Строка
// *Колонки - Массив ИЗ Структура ИЗ
// **этоГруппа - Строка (для создании группы в иерархическом справочнике)
// **этоКлюч - Строка (для поиска элемента справочника)
// **путь - Строка (имя реквизита в который будут добавлены текущие данные, это необходимо из-за группы)
// **имя - Строка
// **тип - ОписаниеТипов (пример: Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(19)) )
// **ширина - Число
//
&НаСервере
Процедура ДобавитьНаФормуРеквизитТаблицаЗначенийНаСервере(описаниеТаблицы)
		
	// Создание таблицы на форме происходит через описание таблицы значений как реквизита:	
	МассивТипРеквизита = Новый Массив;	
	МассивТипРеквизита.Добавить(Тип("ТаблицаЗначений"));
	
	ОписаниеТипаРеквизита 	= Новый ОписаниеТипов(МассивТипРеквизита);
	МассивРеквизитовФормы	= Новый Массив;
	
	МассивРеквизитовФормы.Добавить(
		Новый РеквизитФормы(описаниеТаблицы.Наименование, ОписаниеТипаРеквизита, "", описаниеТаблицы.Заголовок)
	);
	
	// Создаем таблицу:
	ТЗ = Новый ТаблицаЗначений;	
	Для Каждого Колонка Из описаниеТаблицы.Колонки Цикл
		ТЗ.Колонки.Добавить(Колонка.имя, Колонка.тип);
	КонецЦикла;	
	
	// Описываем созданной выше таблицей нашу программную таблицу формы:
	Для Каждого Колонка Из ТЗ.Колонки Цикл		
		МассивРеквизитовФормы.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, описаниеТаблицы.Наименование));		
	КонецЦикла;
	ИзменитьРеквизиты(МассивРеквизитовФормы);
	
КонецПроцедуры

// Процедура Добавить На Форму Команду Загрузки
// имя			- Строка
// заголовок	- Строка
// имяПроцедуры	- Строка - имя процедуры которая будет вызвана (пример: Процедура ДобавитьВСправочник(Команда))
//
&НаСервере
Процедура ДобавитьНаФормуКомандуЗагрузки(имя, заголовок, имяПроцедуры)

	// Команда "аналог реквизит формы"
	НоваяКоманда 			= ЭтаФорма.Команды.Добавить(имя);
	НоваяКоманда.Заголовок	= заголовок;
	НоваяКоманда.Действие	= имяПроцедуры;
	
	// Комадна "аналог элемент формы"
	НовыйЭлемент 					= ЭтаФорма.Элементы.Добавить(имя, Тип("КнопкаФормы"), ЭтаФорма.КоманднаяПанель);
	НовыйЭлемент.ИмяКоманды			= имя;
	НовыйЭлемент.Заголовок			= заголовок;
	НовыйЭлемент.КнопкаПоУмолчанию	= Истина;

КонецПроцедуры

// ВАЖНО: Если справочник Иерархический Тогда колонка "этоГруппа"
// обязана быть описанна первой (и получается находиться в ТЗ пекрвой)
// (это необходимо для создания и записи родителя перед добавлением элементу!)
//
// Колонки которые являются "этоКлюч" = Истина - будет создавать элемент/группу
//
&НаСервереБезКонтекста
Функция ПолучитьОписаниеКолонокНаСервереБезКонтекста()
	
	описаниеТаблицы = Новый Структура;
	
	описаниеТаблицы.Вставить("Наименование");
	описаниеТаблицы.Вставить("Заголовок");
	описаниеТаблицы.Вставить("Колонки");
	
	описаниеТаблицы.Наименование	= "ТабЗначений";
	описаниеТаблицы.Заголовок		= "Таб. значений";
	описаниеТаблицы.Колонки			= Новый Массив;	
	
	// этоГруппа поэтому описываем первой
	описаниеКолонокиОписаниеИмяГруппы = Новый Структура;
    описаниеКолонокиОписаниеИмяГруппы.Вставить("этоГруппа", Истина);
	описаниеКолонокиОписаниеИмяГруппы.Вставить("этоКлюч", Истина);
	описаниеКолонокиОписаниеИмяГруппы.Вставить("путь", "Наименование");
	описаниеКолонокиОписаниеИмяГруппы.Вставить("имя", "ИмяГруппы");
	описаниеКолонокиОписаниеИмяГруппы.Вставить("тип", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)) );
	описаниеКолонокиОписаниеИмяГруппы.Вставить("ширина", 32);
	описаниеТаблицы.Колонки.Добавить(описаниеКолонокиОписаниеИмяГруппы);
	
	описаниеКолонокиДолжность = Новый Структура;
    описаниеКолонокиДолжность.Вставить("этоГруппа", Ложь);
	описаниеКолонокиДолжность.Вставить("этоКлюч", Ложь);
	описаниеКолонокиДолжность.Вставить("путь", "Должность");
	описаниеКолонокиДолжность.Вставить("имя", "Должность");
	описаниеКолонокиДолжность.Вставить("тип", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)) );
	описаниеКолонокиДолжность.Вставить("ширина", 32);
	описаниеТаблицы.Колонки.Добавить(описаниеКолонокиДолжность);
	
	описаниеКолонокиБ1Код = Новый Структура;
    описаниеКолонокиБ1Код.Вставить("этоГруппа", Ложь);
	описаниеКолонокиБ1Код.Вставить("этоКлюч", Истина);
	описаниеКолонокиБ1Код.Вставить("путь", "Наименование");
	описаниеКолонокиБ1Код.Вставить("имя", "Б1Код");
	описаниеКолонокиБ1Код.Вставить("тип", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(19)) );
	описаниеКолонокиБ1Код.Вставить("ширина", 12);
	описаниеТаблицы.Колонки.Добавить(описаниеКолонокиБ1Код);
	
	описаниеКолонокиОписаниеДолжности = Новый Структура;
    описаниеКолонокиОписаниеДолжности.Вставить("этоГруппа", Ложь);
	описаниеКолонокиОписаниеДолжности.Вставить("этоКлюч", Ложь);
	описаниеКолонокиОписаниеДолжности.Вставить("путь", "ОписаниеДолжности");
	описаниеКолонокиОписаниеДолжности.Вставить("имя", "ОписаниеДолжности");
	описаниеКолонокиОписаниеДолжности.Вставить("тип", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(450)) );
	описаниеКолонокиОписаниеДолжности.Вставить("ширина", 100);
	описаниеТаблицы.Колонки.Добавить(описаниеКолонокиОписаниеДолжности);
		
	Возврат описаниеТаблицы;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОтобразитьЗаголовокТабличногоДокумента(ТабДокумент)
		
	описаниеТаблицы = ПолучитьОписаниеКолонокНаСервереБезКонтекста();
	
	номерСтроки		= 1;
	итераторКолонок = 0;
	
	Для Каждого Колонка Из описаниеТаблицы.Колонки Цикл
		итераторКолонок 			= итераторКолонок + 1;
		
		колонкаТабДок				= ТабДокумент.Область("R" + Формат(номерСтроки,"ЧГ=") + "C" + Формат(итераторКолонок,"ЧГ="));
		колонкаТабДок.Текст 		= Колонка.имя;
		колонкаТабДок.ШиринаКолонки	= Колонка.ширина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция СвязатьОписаниеТабЗначСЯчейкамиТабДокПоОписаниюТаблицы(описаниеТаблицы, номерСтроки, ТабДокумент)	
	
	результат		= Новый Массив;
	итераторКолонок = 0;
	
	Для Каждого описание Из описаниеТаблицы.Колонки Цикл
		
		итераторКолонок	= итераторКолонок + 1;		
		ячейкаТабДок	= ТабДокумент.Область("R" + Формат(номерСтроки,"ЧГ=") + "C" + Формат(итераторКолонок,"ЧГ="));
		
		описаниеЯчейки  = Новый Структура;
		
		описаниеЯчейки.Вставить("Описание", описание);
		описаниеЯчейки.Вставить("Ячейка",   ячейкаТабДок);
		
		результат.Добавить(описаниеЯчейки);
		
	КонецЦикла;
	
	Возврат результат;
	
КонецФункции

&НаСервере
Функция ДобавитьВСправочникНаСервере()
	
	описаниеТаблицы = ПолучитьОписаниеКолонокНаСервереБезКонтекста();
	Возврат Справочники.КодыБ1.НаполнитьСправочникДаннымиТабДокумента(описаниеТаблицы, ЭтаФорма.ТабЗначений);
	
КонецФункции

#Область ПереборТабличногоДокументаНаКлиентеЗаполняяТЗ

// Для быстрой загрузки данных табличного документа в таблицу значений.
// Заголовок колонок табДок должен соответствовать наименованию колонок Таблицы значений.
//
// Параметры:
//   ТабДокумент - Коллекция табличный документ.
//   ТаблицаЗначенийПриемник - Коллекция таблица значений.
//
// Пример:
//   ПереборТабличногоДокументаНаКлиентеЗаполняяТЗ(ТабДокумент, ТабЗначений);
//
&НаКлиенте
Процедура ПереборТабличногоДокументаНаКлиентеЗаполняяТЗ(ТабДокумент, ТабЗначений)
	
	ПоследняяСтрока  = ТабДокумент.ВысотаТаблицы;
	ПоследняяКолонка = ТабДокумент.ШиринаТаблицы;
	
	
	///////////////////////////////////////////////////
	описаниеТаблицы 			= ПолучитьОписаниеКолонокНаСервереБезКонтекста();	
	строкаТабличногоДокумента 	= СвязатьОписаниеТабЗначСЯчейкамиТабДокПоОписаниюТаблицы(описаниеТаблицы, 1, ТабДокумент);
	
	ДобавилосьДанных		= 0;
	итераторКолонокТабДок 	= 0;
	Для Каждого стр Из строкаТабличногоДокумента Цикл
		итераторКолонокТабДок = итераторКолонокТабДок + 1;
		
		// Заголовок всегда должен быть заполнен!
		Если Не ЗначениеЗаполнено(стр.Ячейка.Текст) Тогда
			Прервать;
		КонецЕсли;
		
		// Если на мести заголовка данные - переносим их в конец!
		Если стр.Ячейка.Текст <> стр.описание.имя Тогда
			стр.Ячейка.Текст 		= стр.описание.имя;	
			подвалКолонкаХ 			= ТабДокумент.Область(
				"R" + Формат(ПоследняяСтрока + 1,"ЧГ=") + "C" + Формат(итераторКолонокТабДок,"ЧГ=")
			);
			подвалКолонкаХ.Текст 	= стр.Ячейка.Текст;			
			ДобавилосьДанных 		= 1;
		КонецЕсли;
		
	КонецЦикла;

	ПоследняяСтрока = ПоследняяСтрока + ДобавилосьДанных;
	ОтобразитьЗаголовокТабличногоДокумента(ТабДокумент);
	///////////////////////////////////////////////////
		
	Для СтрокаТабДок = 2 По ПоследняяСтрока Цикл		
		
		НоваяСтр 			= Новый Структура;		
		колонкиЗаполнены	= Истина;
		итераторКолонок 	= 0;
		
		Для Каждого колонки Из строкаТабличногоДокумента Цикл
			
			итераторКолонок = итераторКолонок + 1;
			НоваяСтр.Вставить(колонки.описание.имя);
			
			имяКолонки = ТабДокумент.Область(1, итераторКолонок).Текст;
			НоваяСтр[имяКолонки] = ПолучитьСодержаниеЯчейкиТабличногоДокумента(ТабДокумент, СтрокаТабДок, итераторКолонок);
			
			Если НЕ ЗначениеЗаполнено(НоваяСтр[имяКолонки]) Тогда
				колонкиЗаполнены = Ложь;
				Продолжить;
			КонецЕсли;
		
		КонецЦикла;

		Если НЕ колонкиЗаполнены Тогда
			Продолжить;
		КонецЕсли;		
		
		НоваяСтрТЗ = ТабЗначений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрТЗ, НоваяСтр);		
			
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Функция ПолучитьСодержаниеЯчейкиТабличногоДокумента(пТабличныйДокумент, Строка, Колонка)	
	Если Колонка = 0 Тогда
		Возврат "";
	КонецЕсли;	
	мСодержание = пТабличныйДокумент.Область("R" + Формат(Строка,"ЧГ=") + "C" + Формат(Колонка,"ЧГ=")).Текст;	
	Возврат СокрЛП(мСодержание);
КонецФункции

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ПараметрыЗакрытия = ПолучитьПарамертрыЗакрытия();
	ЭтаФорма.Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПарамертрыЗакрытия()
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("СправочникОбнавлен", 	ЭтаФорма.СправочникОбнавлен);
	ПараметрыЗакрытия.Вставить("колСоздан", 			ЭтаФорма.колСоздан);
	ПараметрыЗакрытия.Вставить("колИзменен", 			ЭтаФорма.колИзменен);
	
	Возврат ПараметрыЗакрытия;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

4. Открываем модуль менеджер справочника и копируем код:
#Область КодМодульМенеджераОбъекта

// Пример модуля менеджера объекта
// (это например Справочник.Номенклатура):
// 2. Просто добавим этот код в модуль менеджена нужного объекта

#Область ОписаниеПеременных
#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция НаполнитьСправочникДаннымиТабДокумента(описаниеТаблицы, ТабЗначений) Экспорт
	
	результат = Новый Структура;
	результат.Вставить("колСоздан");
	результат.Вставить("колИзменен");
	
	результат.колСоздан		= 0;
	результат.колИзменен	= 0;
	
	ключевыеКолонки 		= ПолучитьКлючИзСтрокиКолонок(описаниеТаблицы.Колонки);
	
	Если ключевыеКолонки.Количество() = 0 Тогда
		Возврат результат;
	КонецЕсли;
	
	Для Каждого стрТЗ Из ТабЗначений Цикл		
		Для каждого ключ Из ключевыеКолонки Цикл		
			_обкы = ПолучитьОбкПоРеквизитуНаименование(ключ.этоГруппа, ключ.путь, стрТЗ[ключ.имя]);
			
			Для каждого _обк Из _обкы Цикл			
				
				Если _обк.знч.ЭтоГруппа Тогда
					// Ключ обязан содержать имя реквизита обязательного для заполнения!!!
					// ("Наименование", "Код")
					_обк.знч[ключ.путь]	= стрТЗ[ключ.имя];
				Иначе

					Для Каждого колонка Из описаниеТаблицы.Колонки Цикл
						// Для иерархического справочника!
						Если колонка.этоГруппа Тогда
							_обк.знч.Родитель	= Справочники.КодыБ1.НайтиПоНаименованию(стрТЗ[колонка.имя], Истина);
							Продолжить;
						КонецЕсли;
						
						_обк.знч[колонка.путь] 	= стрТЗ[колонка.имя];						
					КонецЦикла;
					
				КонецЕсли;
				
				результат.колСоздан		= ?(_обк.этоНовый, результат.колСоздан + 1, результат.колСоздан);
				результат.колИзменен	= ?(_обк.этоНовый, результат.колИзменен, результат.колИзменен + 1);
				
				_обк.знч.Записать();
			КонецЦикла;			
		КонецЦикла;
	КонецЦикла;
		
	Возврат результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьКлючИзСтрокиКолонок(строкаКолонок)
	
	результат = Новый Массив;
	Для каждого колонка Из строкаКолонок Цикл		
		Если колонка.ЭтоКлюч Тогда
		
			результат.Добавить(колонка);
		
		КонецЕсли;			
	КонецЦикла;
	
	Возврат результат;

КонецФункции // ПолучитьКлючИзСтрокиКолонок()

Функция ПолучитьОбкПоРеквизитуНаименование(этоГруппа, имяРеквизита, знчРеквизита)
	
	результат = Новый Массив;
	
	// Выбор +++
	Запрос = Новый Запрос;
	Запрос.Текст = 
	СтрШаблон(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КодыБ1.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КодыБ1 КАК КодыБ1
		|ГДЕ
		|	КодыБ1.ЭтоГруппа = &ЭтоГруппа
		|	И КодыБ1.%1 = &знчРеквизита",
	имяРеквизита);
	
	Запрос.УстановитьПараметр("знчРеквизита", знчРеквизита);
	Запрос.УстановитьПараметр("ЭтоГруппа", ЭтоГруппа);
	
	РезультатЗапроса 		= Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи	= РезультатЗапроса.Выбрать();
	// Выбор ---
	
	Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда
		_обк 			= Неопределено;
		описаниеОбк 	= Новый Структура;
		
		описаниеОбк.Вставить("ЭтоНовый");
		описаниеОбк.Вставить("знч");
		
		Если этоГруппа Тогда
			_обк = Справочники.КодыБ1.СоздатьГруппу();		
		Иначе
			_обк = Справочники.КодыБ1.СоздатьЭлемент();
		КонецЕсли;
		
		_обк[имяРеквизита] = знчРеквизита;
		_обк.Записать();
		
		описаниеОбк.ЭтоНовый	= Истина;
		описаниеОбк.знч 		= _обк;
		
		результат.Добавить(описаниеОбк);
		
		Возврат результат;
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		__обк 		= ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		описаниеОбк	= Новый Структура;
		
		описаниеОбк.Вставить("ЭтоНовый");
		описаниеОбк.Вставить("знч");		

		описаниеОбк.ЭтоНовый	= Ложь;
		описаниеОбк.знч			= __обк;
		
		результат.Добавить(описаниеОбк);
	КонецЦикла;
	
	Возврат результат;
	
КонецФункции

#КонецОбласти

#Область Инициализация
#КонецОбласти

#КонецОбласти

5. Связываем в каждой форме событие формы "ПриСозданииНаСервере"

Готово!

см. пример в расширении "ИТТ_673.cfe", реквизиты объекта (справочника) должны совпдать с описаниемТаблицы
см. "формаЗагрузки" -> "ПолучитьОписаниеКолонокНаСервереБезКонтекста()"
